# Fit condition specific params for isoelastic model for each subject using partial pooling of information

model{

##LIKELIHOOD
for (c in 1:nConditions){
for (i in 1:nSubjects){
oneminEta[i,c] = 1 - eta_i[i,c]
for (t in 1:nTrials){
    wLU[i,c,t]    = w[i,c,t] + dwLU[i,c,t]
    wLL[i,c,t]    = w[i,c,t] + dwLL[i,c,t]
    wRU[i,c,t]    = w[i,c,t] + dwRU[i,c,t]
    wRL[i,c,t]    = w[i,c,t] + dwRL[i,c,t]
    u[i,c,t]      = (pow(w[i,c,t],oneminEta[i,c]) - 1) / oneminEta[i,c]
    uLU[i,c,t]    = (pow(wLU[i,c,t],oneminEta[i,c]) - 1) / oneminEta[i,c]
    uLL[i,c,t]    = (pow(wLL[i,c,t],oneminEta[i,c]) - 1) / oneminEta[i,c]
    uRU[i,c,t]    = (pow(wRU[i,c,t],oneminEta[i,c]) - 1) / oneminEta[i,c]
    uRL[i,c,t]    = (pow(wRL[i,c,t],oneminEta[i,c]) - 1) / oneminEta[i,c]
    duLU[i,c,t]   = uLU[i,c,t] - u[i,c,t]
    duLL[i,c,t]   = uLL[i,c,t] - u[i,c,t]
    duRU[i,c,t]   = uRU[i,c,t] - u[i,c,t]
    duRL[i,c,t]   = uRL[i,c,t] - u[i,c,t]
    eduL[i,c,t]   = (duLU[i,c,t] + duLL[i,c,t]) / 2
    eduR[i,c,t]   = (duRU[i,c,t] + duRL[i,c,t]) / 2
    deu[i,c,t]    = eduL[i,c,t] - eduR[i,c,t]
    sdeu[i,c,t]   = -1 * beta_i[i,c] * deu[i,c,t]
    cp[i,c,t]     = 1 / (1 + (exp(sdeu[i,c,t])))
    theta[i,c,t]  = max( 0.000001 , min(0.999999 , cp[i,c,t]) )
    y[i,c,t]      ~ dbern(theta[i,c,t])
}# end of trials
}# end of conditions
}# end of subjects


## PRIORS
for (c in 1:nConditions){
log_beta_g[c]  ~ dnorm(mu_log_beta[c], tau_log_beta[c])
beta_g[c]      = exp(log_beta_g[c])

for (i in 1:nSubjects){
    log_beta_i[i,c]  ~ dnorm(mu_log_beta[c], tau_log_beta[c])
    beta_i[i,c]      = exp(log_beta_i[i,c])
}#end of subjects
}#end of conditions

d_eta_g  ~ dnorm(mu_d_eta,tau_d_eta)
eta_g[1] ~ dnorm(0 + d_eta_g,tau_eta[1])
eta_g[2] ~ dnorm(1 + d_eta_g,tau_eta[2])

for (i in 1:nSubjects){
d_eta_i  ~ dnorm(mu_d_eta,tau_d_eta)
eta_i[i,1] ~ dnorm(0 + d_eta_g,tau_eta[1])
eta_i[i,2] ~ dnorm(1 + d_eta_g,tau_eta[2])
}#end of subjects


##HYPERPRIORS
mu_d_eta    ~ dunif(muEtaL,muEtaH)
tau_d_eta   = pow(sigma_d_eta,-2)
sigma_d_eta ~ dunif(sigmaL,sigmaH)

for (c in 1:nConditions){
    #beta parameter
    mu_log_beta[c]       ~ dunif(muLogBetaL,muLogBetaH)
    tau_log_beta[c]      = pow(sigma_log_beta[c],-2)
    sigma_log_beta[c]    ~ dunif(sigmaL,sigmaH)

    #eta parameter
    tau_eta[c]           = pow(sigma_eta[c],-2)
    sigma_eta[c]         ~ dunif(sigmaL,sigmaH)

}#end of conditions

}