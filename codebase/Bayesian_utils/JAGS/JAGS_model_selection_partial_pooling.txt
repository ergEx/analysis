# Model selection selecting between three different models
# EUT model: eta_i_add = eta_i_mul
# EE model: eta_i_add = 0, eta_i_mul = 1
# EE2 model (weak EE model): eta_i_add < eta_i_mul

model{

##LIKELIHOOD
for (c in 1:nConditions){
for (i in 1:nSubjects){
oneminEta_EUT[i,c] = 1 - eta_i_EUT[i]
oneminEta_EE[i,c] = 1 - eta_i_EE[i,c]
oneminEta_EE2[i,c] = 1 - eta_i_EE2[i,c]

for (t in 1:nTrials){
    #CALCULATE WEALTH UPDATES
    wLU[i,c,t]    = w[i,c,t] + dwLU[i,c,t]
    wLL[i,c,t]    = w[i,c,t] + dwLL[i,c,t]
    wRU[i,c,t]    = w[i,c,t] + dwRU[i,c,t]
    wRL[i,c,t]    = w[i,c,t] + dwRL[i,c,t]

    #EUT
    u_EUT[i,c,t]      = (pow(wp[i,c,t],oneminEta_EUT[i,c]) - 1) / oneminEta_EUT[i,c]
    uLU_EUT[i,c,t]    = (pow(wLUp[i,c,t],oneminEta_EUT[i,c]) - 1) / oneminEta_EUT[i,c]
    uLL_EUT[i,c,t]    = (pow(wLLp[i,c,t],oneminEta_EUT[i,c]) - 1) / oneminEta_EUT[i,c]
    uRU_EUT[i,c,t]    = (pow(wRUp[i,c,t],oneminEta_EUT[i,c]) - 1) / oneminEta_EUT[i,c]
    uRL_EUT[i,c,t]    = (pow(wRLp[i,c,t],oneminEta_EUT[i,c]) - 1) / oneminEta_EUT[i,c]
    duLU_EUT[i,c,t]   = uLU_EUT[i,c,t] - u_EUT[i,c,t]
    duLL_EUT[i,c,t]   = uLL_EUT[i,c,t] - u_EUT[i,c,t]
    duRU_EUT[i,c,t]   = uRU_EUT[i,c,t] - u_EUT[i,c,t]
    duRL_EUT[i,c,t]   = uRL_EUT[i,c,t] - u_EUT[i,c,t]
    eduL_EUT[i,c,t]   = (duLU_EUT[i,c,t] + duLL_EUT[i,c,t]) / 2
    eduR_EUT[i,c,t]   = (duRU_EUT[i,c,t] + duRL_EUT[i,c,t]) / 2
    deu_EUT[i,c,t]    = eduL_EUT[i,c,t] - eduR_EUT[i,c,t]
    sdeu_EUT[i,c,t]   = -1 * beta_i_EUT[i,c] * deu_EUT[i,c,t]
    cp_EUT[i,c,t]     = 1 / (1 + (exp(sdeu_EUT[i,c,t])))
    theta[i,c,t,1]     = max(0.000001,min(0.999999,cp_EUT[i,c,t]))

    #EE
    u_EE[i,c,t]      = (pow(wp[i,c,t],oneminEta_EE[i,c]) - 1) / oneminEta_EE[i,c]
    uLU_EE[i,c,t]    = (pow(wLUp[i,c,t],oneminEta_EE[i,c]) - 1) / oneminEta_EE[i,c]
    uLL_EE[i,c,t]    = (pow(wLLp[i,c,t],oneminEta_EE[i,c]) - 1) / oneminEta_EE[i,c]
    uRU_EE[i,c,t]    = (pow(wRUp[i,c,t],oneminEta_EE[i,c]) - 1) / oneminEta_EE[i,c]
    uRL_EE[i,c,t]    = (pow(wRLp[i,c,t],oneminEta_EE[i,c]) - 1) / oneminEta_EE[i,c]
    duLU_EE[i,c,t]   = uLU_EE[i,c,t] - u_EE[i,c,t]
    duLL_EE[i,c,t]   = uLL_EE[i,c,t] - u_EE[i,c,t]
    duRU_EE[i,c,t]   = uRU_EE[i,c,t] - u_EE[i,c,t]
    duRL_EE[i,c,t]   = uRL_EE[i,c,t] - u_EE[i,c,t]
    eduL_EE[i,c,t]   = (duLU_EE[i,c,t] + duLL_EE[i,c,t]) / 2
    eduR_EE[i,c,t]   = (duRU_EE[i,c,t] + duRL_EE[i,c,t]) / 2
    deu_EE[i,c,t]    = eduL_EE[i,c,t] - eduR_EE[i,c,t]
    sdeu_EE[i,c,t]   = -1 * beta_i_EE[i,c] * deu_EE[i,c,t]
    cp_EE[i,c,t]     = 1 / (1 + (exp(sdeu_EE[i,c,t])))
    theta[i,c,t,2]     = max(0.000001,min(0.999999,cp_EE[i,c,t]))

    #EE2
    u_EE2[i,c,t]      = (pow(wp[i,c,t],oneminEta_EE2[i,c]) - 1) / oneminEta_EE2[i,c]
    uLU_EE2[i,c,t]    = (pow(wLUp[i,c,t],oneminEta_EE2[i,c]) - 1) / oneminEta_EE2[i,c]
    uLL_EE2[i,c,t]    = (pow(wLLp[i,c,t],oneminEta_EE2[i,c]) - 1) / oneminEta_EE2[i,c]
    uRU_EE2[i,c,t]    = (pow(wRUp[i,c,t],oneminEta_EE2[i,c]) - 1) / oneminEta_EE2[i,c]
    uRL_EE2[i,c,t]    = (pow(wRLp[i,c,t],oneminEta_EE2[i,c]) - 1) / oneminEta_EE2[i,c]
    duLU_EE2[i,c,t]   = uLU_EE2[i,c,t] - u_EE2[i,c,t]
    duLL_EE2[i,c,t]   = uLL_EE2[i,c,t] - u_EE2[i,c,t]
    duRU_EE2[i,c,t]   = uRU_EE2[i,c,t] - u_EE2[i,c,t]
    duRL_EE2[i,c,t]   = uRL_EE2[i,c,t] - u_EE2[i,c,t]
    eduL_EE2[i,c,t]   = (duLU_EE2[i,c,t] + duLL_EE2[i,c,t]) / 2
    eduR_EE2[i,c,t]   = (duRU_EE2[i,c,t] + duRL_EE2[i,c,t]) / 2
    deu_EE2[i,c,t]    = eduL_EE2[i,c,t] - eduR_EE2[i,c,t]
    sdeu_EE2[i,c,t]   = -1 * beta_i_EE2[i,c] * deu_EE2[i,c,t]
    cp_EE2[i,c,t]     = 1 / (1 + (exp(sdeu_EE2[i,c,t])))
    theta[i,c,t,3]     = max(0.000001,min(0.999999,cp_EE2[i,c,t]))

    #choice
    y[i,c,t]           ~ dbern(theta[i,c,t,z[i]])

}# end of trials
}# end of conditions
}# end of subjects


## PRIORS
#model indicator
for (i in 1:nSubjects){
    z[i] ~ dcat(pz[])
}

#submodels

for (i in 1:nSubjects){
    eta_i_EUT[i]       ~ dnorm(mu_eta_EUT,tau_eta_EUT)

for (c in 1:nConditions){
    eta_i_EE[i,c]       ~ dnorm(mu_eta_EE[c],tau_eta_EE)
    eta_i_EE2[i,c]       ~ dnorm(mu_eta_EE2[c],tau_eta_EE2)

    beta_i_EUT[i,c]      = exp(log_beta_i_EUT[i,c])
    log_beta_i_EUT[i,c]  ~ dnorm(mu_log_beta_EUT[c], tau_log_beta_EUT[c])

    beta_i_EE[i,c]      = exp(log_beta_i_EE[i,c])
    log_beta_i_EE[i,c]  ~ dnorm(mu_log_beta_EE[c], tau_log_beta_EE[c])

    beta_i_EE2[i,c]      = exp(log_beta_i_EE2[i,c])
    log_beta_i_EE2[i,c]  ~ dnorm(mu_log_beta_EE[c], tau_log_beta_EE2[c])

  }#end of subjects
}#end of conditions


##HYPERPRIORS
for (c in 1:nConditions){
    #EUT
    mu_log_beta_EUT[c]       ~ dunif(muLogBetaL, muLogBetaH)
    sigma_log_beta_EUT[c]    ~ dunif(sigmaL,sigmaH)
    tau_log_beta_EUT[c]      = pow(sigma_log_beta_EUT[c],-2)

    #EE
    mu_log_beta_EE[c]       ~ dunif(muLogBetaL, muLogBetaH)
    sigma_log_beta_EE[c]   ~ dunif(sigmaL,sigmaH)
    tau_log_beta_EE[c]       = pow(sigma_log_beta_EE[c],-2)

    #EE2
    mu_log_beta_EE2[c]       ~ dunif(muLogBetaL, muLogBetaH)
    sigma_log_beta_EE2[c]   ~ dunif(sigmaL,sigmaH)
    tau_log_beta_EE2[c]       = pow(sigma_log_beta_EE2[c],-2)

}#end of conditions

#EUT
mu_eta_EUT    ~ dunif(muEtaL , muEtaH)
sigma_eta_EUT ~ dunif(sigmaL, sigmaH)
tau_eta_EUT   = pow(sigma_eta_EUT,-2)

#EE
mu_eta_EE[1] = muEtaEEAdd
mu_eta_EE[2] = muEtaEEMul
sigma_eta_EE ~ dunif(sigmaL, sigmaH)
tau_eta_EE   = pow(sigma_eta_EE,-2)

#EE2
mu_eta_EE2[1]    ~ dunif(muEtaL, muEtaH)
d_mu_eta_EE2 ~ dgamma(dMuAlpha, dMuBeta)
mu_eta_EE2[2] = mu_eta_EE2[1]  + d_mu_eta_EE2

sigma_eta_EE2 ~ dunif(sigmaL,sigmaH)
tau_eta_EE2  = pow(sigma_eta_EE2,-2)

}

